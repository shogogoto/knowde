// 入力するテキストファイルの文法定義
?start:  _NL* unit*  // root

?unit: block|h1

h1: H1 _NL [_INDENT source_about? block* _DEDENT] (h2)*
source_about: [AUTHOR _NL] [PUBLISHED _NL]

h2: H2 _NL [_INDENT block* _DEDENT] (h3)*
h3: H3 _NL [_INDENT block* _DEDENT] (h4)*
h4: H4 _NL [_INDENT block* _DEDENT] (h5)*
h5: H5 _NL [_INDENT block* _DEDENT] (h6)*
h6: H6 _NL [_INDENT block* _DEDENT]

?block: stmt [_INDENT (block)+ _DEDENT]


?stmt: (COMMENT|line|alias_line|define|name|ctx) _NL
line: (ONELINE|MULTILINE)
alias_line: ALIAS line
name: ALIAS? ONELINE ("=" ONELINE)* ":"
define: name line


?ctx.1: (THUS|CAUSE|ANTONYM|EXAMPLE|GENERAL|REF|NUM|SIMILAR) (line|alias_line|define|name)

//------------------------------------------------ terminals
ANY: /[^\n]*/
W: /[^\n:=\$]/     // Word
S: /[^\!#@\n:=\$]/  // 最初の文字
F: /\$[^\n\$]+\$/

//ONELINE: S W*
ONELINE: (F|S) (F|W)*
MULSEP: /\\\n[\t ]*/
MULTILINE.1: (ONELINE MULSEP)+ ONELINE
COMMENT: "!" ANY

ALIAS.2: ONELINE "|"
REFVAR: "{" ONELINE "}"


AUTHOR: "@author " ANY       //著者
PUBLISHED: "@published " ANY // 第一出版日
// 文脈
THUS.1:    "->"   // 故に
CAUSE.1:   "<-"   // なぜなら
ANTONYM.1: "<->"  // 対義関係
EXAMPLE.1: "e.g." // 例化
GENERAL.1: "g.e." // 汎化 例化の逆順
REF.1: "ref."     // 引用
NUM.1: /\d+\./    // 連番
SIMILAR.1: "~="   // 類似

WH: /[^\n#]+/ // Word of Heading
H1: /#/      WH
H2: /##/     WH
H3: /###/    WH
H4: /####/   WH
H5: /#####/  WH
H6: /######/ WH

_NL: /(\r?\n[\t ]*)+/
%declare _INDENT _DEDENT
%import common.WS_INLINE
%import common.CNAME
%ignore _NL COMMENT
