// 入力するテキストファイルの文法定義
?start:  _NL* unit*  // root

?unit: block|h1

h1: H1 _NL [_INDENT source_about? block* _DEDENT] (h2)*
source_about: [AUTHOR _NL] [PUBLISHED _NL]

h2: H2 _NL [_INDENT block* _DEDENT] (h3)*
h3: H3 _NL [_INDENT block* _DEDENT] (h4)*
h4: H4 _NL [_INDENT block* _DEDENT] (h5)*
h5: H5 _NL [_INDENT block* _DEDENT] (h6)*
h6: H6 _NL [_INDENT block* _DEDENT]

?block: stmt [_INDENT (block)+ _DEDENT]


?stmt: (COMMENT|declare|ctx) _NL
line: (ONELINE|MULTILINE)
alias_line.1: ALIAS line
name: ALIAS? ONELINE ("=" ONELINE)* ":"
define: name line
?declare: line|alias_line|define|name


?ctx.2: (THUS|CAUSE|ANTONYM|EXAMPLE|GENERAL|REF|NUM|SIMILAR) declare
//------------------------------------------------ terminals
ANY: /[^\n]*/
W: /[^\n:=\$]/     // Word
S: /[^\!#@\n:=\$]/  // 最初の文字
F: /\$[^\n\$]+\$/

//ONELINE: S W*
ONELINE: (F|S) (F|W)*
MULSEP: /\\\n[\t ]*/
MULTILINE: (ONELINE MULSEP)+ ONELINE
COMMENT: "!" ANY

ALIAS.1: ONELINE "|"
REFVAR: "{" ONELINE "}"


AUTHOR: "@author " ANY       //著者
PUBLISHED: "@published " ANY // 第一出版日
// 文脈
THUS.2:    "->"   // 故に
CAUSE.2:   "<-"   // なぜなら
ANTONYM.2: "<->"  // 対義関係
EXAMPLE.2: "e.g." // 例化
GENERAL.2: "g.e." // 汎化 例化の逆順
REF.2: "ref."     // 引用
NUM.2: /\d+\./    // 連番
SIMILAR.2: "~="   // 類似
BY.2: "by."       // アクターは誰
WHEN.2: "when."   // いつ


WH: /[^\n#]+/ // Word of Heading
H1: /#/      WH
H2: /##/     WH
H3: /###/    WH
H4: /####/   WH
H5: /#####/  WH
H6: /######/ WH

_NL: /(\r?\n[\t ]*)+/
%declare _INDENT _DEDENT
%import common.WS_INLINE
%import common.CNAME
%ignore _NL COMMENT
