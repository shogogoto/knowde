// 入力するテキストファイルの文法定義
?start:  _NL* unit*  // root
?unit: h1

h1: H1 _NL [_INDENT [source_about _NL]* block* _DEDENT] (h2)*
?source_about: AUTHOR | PUBLISHED | URL
AUTHOR.1: "@author " ANY       //著者
PUBLISHED.1: "@published " ANY // 第一出版日
URL.1: "@url " ANY

h2: H2 _NL [_INDENT block* _DEDENT] (h3)*
h3: H3 _NL [_INDENT block* _DEDENT] (h4)*
h4: H4 _NL [_INDENT block* _DEDENT] (h5)*
h5: H5 _NL [_INDENT block* _DEDENT] (h6)*
h6: H6 _NL [_INDENT block* _DEDENT]
HEAD: /[^#\n]+/
H1: "#" HEAD
H2: "##" HEAD
H3: "###" HEAD
H4: "####" HEAD
H5: "#####" HEAD
H6: "######" HEAD


?block: line [_INDENT (block)+ _DEDENT]
?line: (ONELINE|MULTILINE|COMMENT) _NL
ANY: /[^\n]+/
COMMENT: "!" ANY

ONELINE: /[^!#\n]/ ANY?
MULSEP: /\\\n[\t ]*/
MULTILINE: (ONELINE MULSEP)+ ONELINE

_NL: /(\r?\n[\t ]*)+/
%declare _INDENT _DEDENT
%import common.WS_INLINE
%import common.CNAME
